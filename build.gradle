//file:noinspection GroovyAssignabilityCheck
plugins {
  id 'idea'
  id 'java-library'
  id 'maven-publish'
  id 'org.springframework.boot' version "$springBootVersion"
  id 'io.spring.dependency-management' version "$springDependencyManagementPluginVersion"
}

allprojects {

  apply plugin: 'idea'
  apply plugin: 'java-library'

  java {
    withSourcesJar()
    withJavadocJar()
  }

  [compileJava, javadoc]*.options*.encoding = 'UTF-8'

  group = 'dev.tiltrikt.mapper'
  version = '1.1.2'

  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17

  tasks.named('test') {
    useJUnitPlatform()
  }

  dependencies {
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    compileOnly "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"

    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"
  }

  if (!project.name.contains('example') && project.name != 'mapper') {
    publishing {
      publications {
        "$project.name"(MavenPublication) {
          from components.java
          groupId = project.group
          artifactId = project.name
          version = project.version
        }
      }
    }
  }
}

publishing {
  repositories {
    maven {
      name = "TiltriktAWS"
      url = "http://3.64.29.150:8080/releases"
      credentials {
        username = System.getenv("REPOSILITE_USERNAME")
        password = System.getenv("REPOSILITE_SECRET")
      }
      allowInsecureProtocol = true
    }
  }
}
