plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagementPluginVersion"
}

allprojects {

    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    java {
        withSourcesJar()
        withJavadocJar()
    }

    [compileJava, javadoc]*.options*.encoding = 'UTF-8'

    group = 'dev.tiltrikt'
    version = '0.0.3-SNAPSHOT'

    sourceCompatibility = '17'
    targetCompatibility = '17'

    tasks.named('test') {
        useJUnitPlatform()
    }

    dependencies {
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        compileOnly "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"

        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/tiltrikt/mapper")
                credentials {
                    username = System.getenv("USERNAME")
                    password = System.getenv("TOKEN")
                }
            }
        }
        publications {
            mapper(MavenPublication) {
                from(components.java)

                groupId = project.group
                artifactId = project.name
                version = project.version

                pom {
                    description = 'Mapper'
                }
            }
        }
    }
}
